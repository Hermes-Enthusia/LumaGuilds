plugins {
    kotlin("jvm") version "2.0.0"
    id("com.gradleup.shadow") version "8.3.6"
    idea
}

group = "net.lumalyte.lg"
version = "2.1.0"

repositories {
    mavenLocal()
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://repo.aikar.co/content/groups/aikar/")
    maven("https://jitpack.io")
    maven("https://repo.codemc.io/repository/maven-snapshots/")
    maven("https://repo.opencollab.dev/main/")
    maven("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    maven {
        name = "artillex-studios"
        url = uri("https://repo.artillex-studios.com/releases/")
    }
    maven {
        name = "sirblobman-public"
        url = uri("https://nexus.sirblobman.xyz/public/")
    }
    maven {
        name = "lunarclient-public"
        url = uri("https://repo.lunarclient.dev/")
    }
    // Sonatype OSS snapshots — kept as last-resort fallback because
    // the domain has frequent outages (HTTP 504).  All key SNAPSHOT
    // deps are covered by dedicated repos above:
    //   - PlaceholderAPI → JitPack
    //   - Geyser / Floodgate / Cumulus → OpenCollab
    //   - ACF / IDB → Aikar
    //   - CombatLogX → SirBlobman
    maven("https://oss.sonatype.org/content/repositories/snapshots")
}

dependencies {
    testImplementation(kotlin("test"))
    testImplementation("io.mockk:mockk:1.13.11")
    testImplementation("org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.107.0")
    testImplementation("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    testImplementation("com.github.MilkBowl:VaultAPI:1.7") {
        exclude(group = "org.bukkit", module = "bukkit")
    }
    testImplementation("org.junit.jupiter:junit-jupiter:5.8.1")
    testImplementation("org.xerial:sqlite-jdbc:3.45.1.0")

    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    shadow("org.jetbrains.kotlin:kotlin-stdlib")

    implementation("org.slf4j:slf4j-nop:2.0.13")
    implementation("com.zaxxer:HikariCP:5.1.0")
    implementation("org.mariadb.jdbc:mariadb-java-client:3.3.2")
    implementation("co.aikar:acf-paper:0.5.1-SNAPSHOT")
    implementation("co.aikar:idb-core:1.0.0-SNAPSHOT")
    implementation("com.github.stefvanschie.inventoryframework:IF:0.11.6")
    implementation("io.insert-koin:koin-core:4.0.2")
    implementation("org.json:json:20240303")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.10.2")

    // QR Code generation
    implementation("com.google.zxing:core:3.5.3")
    implementation("com.google.zxing:javase:3.5.3")

    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        exclude(group = "org.bukkit", module = "bukkit")
    }
    compileOnly("com.github.placeholderapi:placeholderapi:2.11.6")
    compileOnly("com.artillexstudios:AxKothAPI:4")
    // RoseChat is required at compile-time for the GuildChatListener channel switch.
    // Drop the built jar into libs/ from the RoseChat project (libs/ is gitignored).
    compileOnly(files("libs/RoseChat-RC-2.jar"))

    // EnthusiaMarket public API (net.badgersmc.em.api.ShopGuildLookup) for guild-shop
    // integration. Slim api-only jar (one interface, depends only on Bukkit) — NOT the
    // full EM jar, to avoid a circular build dependency (EM builds against LumaGuilds).
    // Committed under libs/ because it's tiny + stable; resolved at runtime from the
    // EnthusiaMarket plugin via Bukkit's ServicesManager.
    compileOnly(files("libs/enthusiamarket-api.jar"))

    // geyser
    compileOnly("org.geysermc.geyser:api:2.9.4-SNAPSHOT")
    compileOnly("org.geysermc.floodgate:api:2.2.5-SNAPSHOT")
    compileOnly("org.geysermc.cumulus:cumulus:2.0.0-SNAPSHOT")

    //adventure
    compileOnly("net.kyori:adventure-api:4.17.0")
    compileOnly("net.kyori:adventure-text-minimessage:4.17.0")

    //combatlogX api
    compileOnly("com.github.sirblobman.api:core:2.9-SNAPSHOT")
    compileOnly("com.github.sirblobman.combatlogx:api:11.6-SNAPSHOT")

    // Lunar Client Apollo API
    compileOnly("com.lunarclient:apollo-api:1.2.3")
    compileOnly("com.lunarclient:apollo-extra-adventure4:1.2.3")

}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

// Keep local tooling trees (e.g. Claude worktree copies) out of the IDE module so
// Kotlin does not see duplicate sources like TeleportationService.kt twice.
idea {
    module {
        excludeDirs.add(file(".claude"))
        excludeDirs.add(file(".worktrees"))
    }
}

tasks.test {
    useJUnitPlatform()
}

tasks.shadowJar {
    archiveBaseName.set("LumaGuilds")
    archiveClassifier.set("")
    archiveVersion.set(version.toString())

    mergeServiceFiles()

    relocate("com.zaxxer.hikari", "net.lumalyte.lg.shaded.hikari")
    relocate("co.aikar.commands", "net.lumalyte.lg.shaded.acf")
    relocate("co.aikar.idb", "net.lumalyte.lg.shaded.idb")

    exclude("META-INF/maven/**")
    exclude("META-INF/versions/**")
    exclude("**/module-info.class")
}

tasks.processResources {
    val props = mapOf("version" to version)
    inputs.properties(props)
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand(props)
    }
}
